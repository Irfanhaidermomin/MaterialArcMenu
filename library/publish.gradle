apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '2.0.0'
group 'com.sa90.materialarcmenu'

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "saurabharora90"
            name "Saurabh Arora"
            email "saurabharora90@gmail.com"
        }
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-release.aar")
            groupId
            artifactId 'library'
            version this.version

            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }

                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'material-arc-menu'
        description = "An android custom view which allows you to have a arc style-menu on your pages"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/saurabharora90/MaterialArcMenu'
        dryRun = false
        version {
            name = this.version
            desc = "${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}